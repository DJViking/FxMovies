import java.time.format.DateTimeFormatter
import java.time.ZoneId

plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id "com.diffplug.gradle.spotless" version "3.15.0"
}

mainClassName = 'no.smeaworks.movies.MoviesApplication'

final Date date = new Date()
final DateTimeFormatter formatter = DateTimeFormatter.ofPattern("yyyy.MM.dd HH:mm:ss")
ext {
    buildDateTime = date.toInstant().atZone(ZoneId.systemDefault()).format(formatter);

    buildRelease = project.hasProperty("release")
    if (!buildRelease) {
        project.version = project.version + "-SNAPSHOT"
    }

    moduleName = 'no.smeaworks.movies'
}

repositories {
    mavenCentral()
}

def currentOS = org.gradle.internal.os.OperatingSystem.current()
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

final def log4jGroup = 'org.apache.logging.log4j'
final def log4jVersion = '2.11.1'

final def junitJupiterGroup = 'org.junit.jupiter'
final def junitJupiterVersion = '5.2.0'

final def junitPlatformGroup = 'org.junit.platform'
final def junitPlatformVersion = '1.2.0'
dependencies {
    compile group: "org.openjfx", name: "javafx-base", version: "11", classifier: platform
    compile group: "org.openjfx", name: "javafx-graphics", version: "11", classifier: platform
    compile group: "org.openjfx", name: "javafx-controls", version: "11", classifier: platform
    compile group: "org.openjfx", name: "javafx-media", version: "11", classifier: platform
    compile group: "org.openjfx", name: "javafx-web", version: "11", classifier: platform
    compile group: "org.openjfx", name: "javafx-fxml", version: "11", classifier: platform

    compile group: log4jGroup, name: 'log4j-api', version: log4jVersion
    compile group: log4jGroup, name: 'log4j-core', version: log4jVersion
    compile group: 'com.lmax', name: 'disruptor', version:'3.3.7'

    testCompile group: junitJupiterGroup, name: 'junit-jupiter-api', version: junitJupiterVersion
    testRuntime group: junitJupiterGroup, name: 'junit-jupiter-engine', version: junitJupiterVersion

    testRuntimeOnly group: junitPlatformGroup, name: 'junit-platform-launcher', version: junitPlatformVersion

    testCompile group: "org.testfx", name: "testfx-core", version: "4.0.14-alpha"
    testCompile group: "org.testfx", name: "testfx-junit5", version: "4.0.14-alpha"
    testCompile group: "org.testfx", name: "openjfx-monocle", version: "jdk-11+26"
}

jar {
    doFirst {
        manifest {
            attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': project.group,
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Specification-Vendor': project.group,
                "Main-Class": mainClassName,
                'Built-By': System.getProperty('user.name'),
                'Build-Time': buildDateTime,
                'Created-By': System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Permissions': 'all-permissions',
                'Codebase': '*'
            )
        }
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()

    reports {
        html.enabled = true
    }
}

apply from: rootProject.file('spotless.gradle')
apply from: rootProject.file('modules.gradle')
